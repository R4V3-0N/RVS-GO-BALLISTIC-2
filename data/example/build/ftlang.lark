start: stmt+

stmt: eq_assign | co_assign | block

block: NAME "{" (stmt | addexpr)* "}"

eq_assign: target EQOP addexpr
co_assign: target ":" WORDS

addexpr: (addexpr ADDOP)? mulexpr

mulexpr: (mulexpr MULOP)? dotexpr

dotexpr: single ("." NAME)*

single: NAME | NUMBER | STRING

target: NAME ("." NAME)*

EQOP: "+=" | "*=" | "-=" | "/=" | "="
ADDOP: "+" | "-"
MULOP: "*" | "//"

WORDS: /[^\n]+/

%ignore SPACE

%ignore /\s*(\/\/|--|#)[^\n]*/

NUMBER: SIGNED_NUMBER | UNSIGNED_NUMBER

%import common.CNAME -> NAME
%import common.NUMBER -> UNSIGNED_NUMBER
%import common.SIGNED_NUMBER -> SIGNED_NUMBER
%import common.ESCAPED_STRING -> STRING
%import common.WS -> SPACE
